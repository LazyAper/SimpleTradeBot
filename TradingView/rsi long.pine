//@version=5

// Объявление стратегии
strategy(title="long SimpleTradeBot", shorttitle="long rsi", initial_capital = 100, default_qty_type = strategy.percent_of_equity, default_qty_value = 100, commission_value = 0.075)

// Ввод исходных значений
capital = input.int(defval=100, title="Капитал", minval = 1, maxval = 1000, step = 1)
period          = input.int(defval=7, title="Период RSI", minval = 1, maxval = 20, step = 1)
buy_level       = input.int(defval=30, title="Порог RSI", minval = 1, maxval = 50, step = 1) 
sell_profit_percent    = input.float(defval=1.5, title="% take profit", minval = 0.0, step = 0.1)
sell_loss_percent    = input.float(defval=0.5, title="% stop loss", minval = 0.0, step = 0.1)
rsi_timeframe   = input.timeframe(title="Таймфрейм", defval="1")
commission    = input.float(defval=0.075, title="комиссия %", minval = 0.0, step = 0.01)
start_time = input.time(defval = timestamp("1 Jan 2023 00:00 +0300"), title="Время начала")
only_one_buy = input.bool(defval = false, title = "не более одной покупки к ряду")


// Ввод параметров расположения таблицы
tableYpos = input.string(defval="middle", title="Позция таблицы по вертикали", inline="11", options=["top", "middle", "bottom"])
tableXpos = input.string(defval="center", title="Позция таблицы по горизонтали", inline="11", options=["left", "center", "right"])

// Получаем RSI
rsi = ta.rsi(close, period)

// Получаем RSI по нужному таймфрейму
rsi_series = request.security(syminfo.tickerid, rsi_timeframe, rsi, gaps=barmerge.gaps_on)

// задаем переменные работы скрипта
var buy_price = 0.0
var open_flag = false

// Условия на покупку и продажу
tp_price=buy_price*(sell_profit_percent+100)/100 //1.015*buy_price
sl_price=buy_price*(100-sell_loss_percent)/100//0.995*buy_price

sell = (time("") >= start_time) and (buy_price != 0) and open_flag and (
 open>=tp_price or open<=sl_price or
 low>=tp_price or low<=sl_price or
 high>=tp_price or high<=sl_price or
 close>=tp_price or close<=sl_price)
buy = (rsi_series <= buy_level) and (time("") > start_time)
if only_one_buy
    buy:=buy and rsi[1]>buy_level

if buy == true and not(open_flag)
    // Покупка
    strategy.entry("buy", strategy.long)
    open_flag := true
if sell == true
    // Продажа
    strategy.close_all(comment = "BUY:" + str.tostring(buy_price) + "|SELL:" + str.tostring(close))
    open_flag := false

// Забираем цену покупки текущего трейда
buy_price := strategy.opentrades.entry_price(strategy.opentrades - 1)

// Выводим RSI внизу
plot(rsi_series)
// Ротображаем линию уровня покупки
hline(buy_level, "Покупка", color.green)

// Создаем таблицу с результатами
var table resultTable = table.new(tableYpos + "_" + tableXpos, 2, 4, bgcolor = color.rgb(255, 255, 255, 0), frame_width = 1, frame_color = color.rgb(0, 0, 0, 0), border_width = 1, border_color = color.rgb(30, 39, 46, 0))

// Пишем результаты в таблицу
// Заголовки
table.cell(resultTable, 0, 0, text="Profit", text_color=color.rgb(30, 39, 46, 0))
table.cell(resultTable, 0, 1, text="Per day", text_color=color.rgb(30, 39, 46, 0))
table.cell(resultTable, 0, 2, text="TP Count", text_color=color.rgb(30, 39, 46, 0))
table.cell(resultTable, 0, 3, text="SL Count", text_color=color.rgb(30, 39, 46, 0))
// Значения
profit = capital * strategy.wintrades * (sell_profit_percent / 100)  - capital * strategy.losstrades * (sell_loss_percent / 100) - 2*commission/100*capital*strategy.closedtrades
var float days = 0
if(bar_index==0)
    days := (timenow-time)/8.64e+7
    if(time < start_time)
        days := (timenow-start_time)/8.64e+7

table.cell(resultTable, 1, 0, text=str.format("{0,number,#.#}", profit) + "(" + str.format("{0,number,#.#}", strategy.netprofit)+")", text_color=color.rgb(30, 39, 46, 0))
table.cell(resultTable, 1, 1, text=str.format("{0,number,#.#}", profit/days) + " (" +str.format("{0,number,#.#}", days) + "d)", text_color=color.rgb(30, 39, 46, 0))

table.cell(resultTable, 1, 2, text=str.tostring(strategy.wintrades), text_color=color.rgb(30, 39, 46, 0))
table.cell(resultTable, 1, 3, text=str.tostring(strategy.losstrades), text_color=color.rgb(30, 39, 46, 0))